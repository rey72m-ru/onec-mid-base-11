
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ++ Пантелеев 19.07.25 Домашнее задание 12-2-1
	ЭлементКонтактноеЛицо = Элементы.Вставить("Нето_КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.Нето_КонтактноеЛицо";
	// -- Пантелеев 19.07.25 Домашнее задание 12-2-1

	// ++ Пантелеев 19.07.25 Домашнее задание 12-2-2
	ГруппаСкидка = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаСкидка.ОтображатьЗаголовок  = Ложь;
	
	ЭлементСкидка = Элементы.Вставить("Нето_Скидка", Тип("ПолеФормы"), Элементы.ГруппаСкидка);
	ЭлементСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСкидка.Заголовок = "Согласованная скидка";
	ЭлементСкидка.ПутьКДанным = "Объект.Нето_Скидка";
	ЭлементСкидка.УстановитьДействие("ПриИзменении", "Нето_СкидкаПриИзменении");
	
	КомандаОбновитьСУчетомСкидки = Команды.Добавить("Пересчитать");
	КомандаОбновитьСУчетомСкидки.Действие = "Нето_Пересчитать";
	КнопкаОбновитьСУчетомСкидки = Элементы.Вставить("Пересчитать", Тип("КнопкаФормы"), Элементы.ГруппаСкидка);
	КнопкаОбновитьСУчетомСкидки.ИмяКоманды = "Пересчитать"; 
	КнопкаОбновитьСУчетомСкидки.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаОбновитьСУчетомСкидки.Отображение = ОтображениеКнопки.Картинка;
	// -- Пантелеев 19.07.25 Домашнее задание 12-2-2
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// ++ Пантелеев 19.07.25 Домашнее задание 12-2-2
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
	КоэффициентСкидки = ТекущиеДанные.Скидка + Объект.Нето_Скидка;
	Если КоэффициентСкидки > 100 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Суммарная скидка на товар ""%1"" составила %2%%. Установлено 100%%", ТекущиеДанные.Номенклатура, КоэффициентСкидки));
		КоэффициентСкидки = 100;	
	КонецЕсли;
	ТекущиеДанные.Сумма = (ТекущиеДанные.Цена - (ТекущиеДанные.Цена * КоэффициентСкидки / 100)) * ТекущиеДанные.Количество;
	РассчитатьСуммуДокумента();
	// -- Пантелеев 19.07.25 Домашнее задание 12-2-2
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Нето_СкидкаПриИзменении(Элемент)
	// ++ Пантелеев 19.07.25 Домашнее задание 12-2-2	
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Вопрос = ВопросАсинх("Пересчитать суммы с учетом новой скидки?", РежимДиалогаВопрос.ДаНет);
	ОтветНаВопрос = Ждать Вопрос;
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ОбновитьТаблицы();
	КонецЕсли;
	// -- Пантелеев 19.07.25 Домашнее задание 12-2-2	
КонецПроцедуры

&НаКлиенте
Процедура Нето_Пересчитать(Команда)
	// ++ Пантелеев 19.07.25 Домашнее задание 12-2-2	
  	ОбновитьТаблицы();
	// -- Пантелеев 19.07.25 Домашнее задание 12-2-2		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицы()
	// ++ Пантелеев 19.07.25 Домашнее задание 12-2-2	
	Для Каждого СтрокаТЧ Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(СтрокаТЧ);
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(СтрокаТЧ);
	КонецЦикла;
	// -- Пантелеев 19.07.25 Домашнее задание 12-2-2	
КонецПроцедуры


#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
